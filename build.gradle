plugins {
    id 'java-library'
    id 'maven-publish'
}

ext {
    defaultEncoding = 'UTF-8'
    defaultJavaVersion = JavaVersion.VERSION_11
    pathLicense = file "$projectDir/LICENSE"
}

group 'raystark.eflib'
version '1.2.1'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar) {
    archiveClassifier.set 'javadoc'
    from javadoc
}

task sourceJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set 'source'
    from sourceSets.main.allSource
}

setSourceCompatibility defaultJavaVersion
setTargetCompatibility defaultJavaVersion

tasks.withType(JavaCompile) {
    options.encoding defaultEncoding
}

tasks.withType(Jar) {
    from pathLicense
}

javadoc {
    options.encoding defaultEncoding
}

build {
    dependsOn sourceJar, javadocJar
}

publishing {

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri "https://maven.pkg.github.com/raystarkmc/ExtendedFunctionLibrary"
            credentials {
                username = getProperty "github.username"
                password = getProperty "github.password"
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            pom {
                url = "https://github.com/RayStarkMC/ExtendedFunctionLibrary"
                description = "test"
                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }

            from(components.java)
        }
    }
}