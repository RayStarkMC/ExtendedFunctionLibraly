plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

group 'raystark.eflib'
version '1.2.1'

ext {
    defaultEncoding = 'UTF-8'
    defaultJavaVersion = JavaVersion.VERSION_11
    pathLicense = file "$projectDir/LICENSE"
}

tasks.withType(JavaCompile) {
    options.encoding defaultEncoding
}

tasks.withType(Jar) {
    from pathLicense
}

javadoc.options.encoding defaultEncoding

java {
    withJavadocJar()
    withSourcesJar()
    setSourceCompatibility defaultJavaVersion
    setTargetCompatibility defaultJavaVersion
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri "https://maven.pkg.github.com/raystarkmc/ExtendedFunctionLibrary"
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            version = "testversion"
            pom {
                name = "ExtendedFunctionLibrary"
                description = "The function library based on java.util.function package."
                url = "https://github.com/RayStarkMC/ExtendedFunctionLibrary"
                licenses {
                    license {
                        name = "MIT"
                    }
                }
            }
            from(components.java)
        }
    }
}
