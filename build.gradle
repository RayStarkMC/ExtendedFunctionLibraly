plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:20.1.0'

    testCompileOnly 'org.jetbrains:annotations:20.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
}

group 'raystark'
version '1.4.0'

ext {
    defaultEncoding = 'UTF-8'
    javaVersion = JavaVersion.VERSION_15
    pathLicense = file "$projectDir/LICENSE"
}

tasks.withType(JavaCompile) {
    options.encoding defaultEncoding
}

tasks.withType(Jar) {
    from pathLicense
}

javadoc {
    options.encoding = defaultEncoding
    options.outputLevel = JavadocOutputLevel.QUIET
}

java {
    withJavadocJar()
    withSourcesJar()
    setSourceCompatibility javaVersion
    setTargetCompatibility javaVersion
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri "https://maven.pkg.github.com/raystarkmc/ExtendedFunctionLibrary"
            credentials {
                username = project.findProperty "github.username"
                password = project.findProperty "github.password"
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            pom {
                name = "ExtendedFunctionLibrary"
                description = "The function library based on java.util.function package."
                url = "https://github.com/RayStarkMC/ExtendedFunctionLibrary"
                licenses {
                    license {
                        name = "MIT"
                    }
                }
            }
            from(components.java)
        }
    }
}