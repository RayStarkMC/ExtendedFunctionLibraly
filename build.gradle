plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
    testImplementation platform('org.junit:junit-bom:5.7.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

group 'raystark'
version '1.2.1'

ext {
    defaultEncoding = 'UTF-8'
    javaVersion = JavaVersion.VERSION_11
    pathLicense = file "$projectDir/LICENSE"
}

tasks.withType(JavaCompile) {
    options.encoding defaultEncoding
}

tasks.withType(Jar) {
    from pathLicense
}

javadoc {
    options.encoding defaultEncoding
}

java {
    withJavadocJar()
    withSourcesJar()
    setSourceCompatibility javaVersion
    setTargetCompatibility javaVersion
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri "https://maven.pkg.github.com/raystarkmc/ExtendedFunctionLibrary"
            credentials {
                username = project.findProperty "github.username"
                password = project.findProperty "github.password"
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            pom {
                name = "ExtendedFunctionLibrary"
                description = "The function library based on java.util.function package."
                url = "https://github.com/RayStarkMC/ExtendedFunctionLibrary"
                licenses {
                    license {
                        name = "MIT"
                    }
                }
            }
            from(components.java)
        }
    }
}